plugins{
    id 'java'
    id 'checkstyle'
    id 'com.github.spotbugs' version '5.2.4'
    id 'jacoco'
    id 'com.diffplug.spotless' version '6.13.0'
}


subprojects {
    apply plugin: 'java'
    apply plugin: 'checkstyle'
    apply plugin: 'com.github.spotbugs'
    apply plugin: 'jacoco'
    apply plugin: 'com.diffplug.spotless'

    repositories {
        mavenLocal()
        mavenCentral()
    }

    test {
        useJUnitPlatform()
        testLogging {
            events 'passed', 'skipped', 'failed'
        }
        finalizedBy jacocoTestReport
    }

    jacocoTestReport {
        dependsOn test
    }

    jacocoTestCoverageVerification {
        violationRules {
            rule {
                limit {
                    minimum = 0.9
                }
            }
        }
    }

    spotless {
        java {
            importOrder()
            removeUnusedImports()
            googleJavaFormat()
            formatAnnotations()
        }
    }



    dependencies {
        testImplementation platform('org.junit:junit-bom:5.10.1')
        testImplementation 'org.junit.jupiter:junit-jupiter'
        testImplementation 'org.slf4j:slf4j-api:2.0.9'
        if (project.name == 'server') {
            implementation 'org.slf4j:slf4j-api:2.0.9'
        } else {
            compileOnly 'org.slf4j:slf4j-api:2.0.9'
        }
    }
}